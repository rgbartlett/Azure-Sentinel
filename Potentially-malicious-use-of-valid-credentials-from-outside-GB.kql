// Potentially malicious use of valid credentials from outside GB
// Looks for new (not seen in the last 7 days) use of valid credentials (failed due to MFA denied but conditional access invoked) outside GB.
let IgnoreCodes = dynamic(["0", "50074","500121","50053","50126"]);
let Travellers = (SigninLogs
| extend StatusCode = tostring(Status.errorCode), StatusDetails = tostring(Status.additionalDetails)
| where TimeGenerated between(ago(7d)..ago(1d))
| where StatusCode == "0"
| where Location !="GB"
| distinct UserPrincipalName);
SigninLogs 
| extend StatusCode = tostring(Status.errorCode), StatusDetails = tostring(Status.additionalDetails)
| extend State = tostring(LocationDetails.state), City = tostring(LocationDetails.city)
| extend Browser = tostring(DeviceDetail.browser)
| extend CondAccPol = tostring(ConditionalAccessPolicies)
| distinct TimeGenerated, UserPrincipalName, AppDisplayName, ResourceDisplayName, Browser, StatusCode, ResultDescription, IPAddress, Location, CondAccPol
| sort by TimeGenerated
// Include 50074 and 500121 events where Conditional Access is triggered, indicating valid credentials
| where (StatusCode == "50074" or StatusCode == "500121") and CondAccPol != "[]" and Location != "GB" and TimeGenerated > ago(4h) 
// Exclude stuff we don't care about because they're indicative of invalid credentials or successful authentication which has passed MFA.
or StatusCode !in~ (IgnoreCodes) and UserPrincipalName !in~ (Travellers) and Location != "GB" and TimeGenerated > ago(4h)