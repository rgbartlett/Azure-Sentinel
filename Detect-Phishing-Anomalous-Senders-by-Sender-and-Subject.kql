// Define phishing links here based on current threat information
let PhishingLinkHost = dynamic(["clicktime.symantec.com","t.indeed.com","slickdeals.net","www.linkedin.com/slink","my.sharepoint.com","bing.com/ck/a","google.com/amp/s/","www.dropbox.com/l/scl/","indd.adobe.com","www.canva.com/design/","000webhostapp.com/","firebaseapp.com/","app.box.com/notes","webcindario.com/",".pl/",".ru/"]);
// Add any links which cause false positives, typically Canva Urls which I've redcated from this query
let AllowedLinks = dynamic(["https://www.canva.com/design"]);
// Define minimum recipients, 5 was based on previous phishing emails
let MinRecipients = 5;
// Build list of senders who have previously sent emails with links
let LinkSenders = (EmailEvents
| where UrlCount > 0 and EmailDirection == "Inbound"
| where TimeGenerated between (ago(30d)..ago(1h)) and DeliveryAction == "Delivered"
| distinct SenderFromAddress);
// Build list of mass email senders based on count of sender address and subject rather than number of recipients
let MassEmailFromAddress = (EmailEvents
| where TimeGenerated > ago(1h) and EmailDirection == "Inbound"
| summarize MessageCount = count() by SenderFromAddress, Subject
| where MessageCount >= 5
| distinct SenderFromAddress, Subject);
// Build list of mass email subjects based on count of sender address and subject
let MassEmailSubject = (EmailEvents
| where TimeGenerated > ago(1h) and EmailDirection == "Inbound"
| summarize MessageCount = count() by SenderFromAddress, Subject
| where MessageCount >= 5
| distinct Subject);
let FoundEmails = (EmailEvents
| where TimeGenerated > ago(1h) and EmailDirection == "Inbound" and UrlCount > 0
| join EmailUrlInfo on NetworkMessageId
| where Url has_any (PhishingLinkHost) and SenderFromAddress !in~ (LinkSenders) and SenderFromAddress in~ (MassEmailFromAddress) and Subject in~ (MassEmailSubject) and Url !contains (AllowedLinks)
| distinct NetworkMessageId);
EmailEvents
| where NetworkMessageId in~ (FoundEmails)
| distinct TimeGenerated, RecipientEmailAddress, SenderFromAddress, Subject, NetworkMessageId